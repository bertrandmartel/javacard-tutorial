// Buildscript configuration for the javacard-gradle plugin.
// Do not modify this particular block. Dependencies for the project are lower.
buildscript {
    repositories {
        mavenCentral()
        maven { url "https://javacard.pro/maven" }
        maven { url "https://deadcode.me/mvn" }
        // mavenLocal()
    }
    dependencies {
        classpath 'com.klinec:gradle-javacard:1.8.0'
    }
}

apply plugin: 'com.klinec.gradle.javacard'
apply plugin: 'idea'
sourceCompatibility = 1.8

// Common settings, definitions
final def rootPath = rootDir.absolutePath
final def libs = rootPath + '/libs'
final def libsSdk = rootPath + '/oracle_javacard_sdks'

// Repositories for your project
repositories {
    mavenCentral()
    // mavenLocal() // for local maven repository if needed

    // Repository with JCardSim, Globalplatform, etc, ...
    maven { url  "https://dl.bintray.com/ph4r05/jcard" }
    maven { url "https://javacard.pro/maven" }
    maven { url "https://deadcode.me/mvn" }

    // Local lib repository
    flatDir {
        dirs libs
    }
}

// Dependencies for your project
dependencies {
    jcardsim 'com.klinec:jcardsim:3.0.5.11'

    testImplementation 'org.testng:testng:6.1.1'
    testImplementation 'org.slf4j:slf4j-api:1.7.2'
    testImplementation 'org.slf4j:slf4j-log4j12:1.7.2'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.11.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.1'

    testImplementation "com.github.martinpaljak:apdu4j-core:2020b1"
    testImplementation "com.github.martinpaljak:apdu4j-pcsc:20.08.12"

    testImplementation 'com.squareup.okhttp3:okhttp:4.9.1'
    testImplementation 'org.json:json:20180130'
    testImplementation 'commons-codec:commons-codec:1.15'

    // Include plugin as it has bundled GP & other tools.
    // Alternative: include GP manually, but the included
    // version has to be compatible with the plugin.
    runtimeOnly 'com.klinec:gradle-javacard:1.8.0'
}

task dumpClassPath(dependsOn: ['idea']) {
    doLast {
        println "Gradle classpath:\n- " + configurations.compile.files*.name.join("\n- ")
        println "-------\n"
        println "IDEA classpath: \n- " + file(project.name + ".iml").readLines()
                .grep(~/.*"jar:.*/).collect { it.split("[\\/]")[-3].trim() }.join("\n- ")
        println "-------\n"
    }
}

test {
    // useTestNG()
    useJUnitPlatform {
        excludeTags 'manual'
    }

    task manualTests(type: Test) {
        useJUnitPlatform {
            includeTags 'manual'
        }
        check.dependsOn it
        shouldRunAfter test
    }
}

// JavaCard SDKs and libraries
final def JC212 = libsSdk + '/jc212_kit'
final def JC221 = libsSdk + '/jc221_kit'
final def JC222 = libsSdk + '/jc222_kit'
final def JC303 = libsSdk + '/jc303_kit'
final def JC304 = libsSdk + '/jc304_kit'
final def JC305 = libsSdk + '/jc305u1_kit'
final def JC305u2 = libsSdk + '/jc305u2_kit'
final def JC305u3 = libsSdk + '/jc305u3_kit'
final def JC310b43 = libsSdk + '/jc310b43_kit'

// Which JavaCard SDK to use - select
// In order to compile JC222 and lower you have to have Java SDK <= 1.8
// For more info on JavaCard vs JDK version requirements inspect:
//   https://github.com/martinpaljak/ant-javacard/wiki/Version-compatibility
//
// JC310b43 supports building also for lower versions (cap.targetsdk).
// If another SDK is selected, please comment the cap.targetsdk setting.
final def JC_SELECTED = JC304

javacard {

    config {

        debugGpPro true

        jckit JC_SELECTED
        cap {
            packageName 'fr.bmartel.passwords'
            version '0.1'
            aid 'A0:00:00:00:00:00:00:AA:AA'
            output 'applet.cap'
            applet {
                className 'fr.bmartel.passwords.PasswordManager'
                aid 'A0:00:00:00:00:00:00:AA:AA:03'
            }
        }
    }

    scripts {
        script {
            name 'select'
            apdu '00:A4:04:00:0A:A0:00:00:00:00:00:00:AA:AA:03'
        }
        script {
            name 'add password entry'
            apdu '00:30:00:00:11:F1:04:48:6F:6D:65:F2:03:62:6F:62:F3:04:70:61:73:73'
        }
        script {
            name 'get password entry'
            apdu '00:32:00:00:06:F1:04:48:6F:6D:65'
        }
        script {
            name 'delete password entry'
            apdu '00:34:00:00:06:F1:04:48:6F:6D:65'
        }
        script {
            name 'list identifiers'
            apdu '00:36:00:00'
        }
        task {
            name 'listPassword'
            scripts 'select', 'list identifiers'
        }
        task {
            name 'addPassword'
            scripts 'select', 'add password entry'
        }
        task {
            name 'removePassword'
            scripts 'select', 'delete password entry'
        }
        task {
            name 'addRemovePassword'
            scripts 'select', 'add password entry', 'delete password entry'
        }
    }
}